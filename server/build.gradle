plugins {
    id 'java'
    id 'scala'
    id 'jacoco'
    alias(libs.plugins.jooq.codegen)
    alias(libs.plugins.download)
    alias(libs.plugins.spring.boot)
    alias(libs.plugins.spring.dependency.management)
    alias(libs.plugins.hibernate.orm)
    alias(libs.plugins.test.logger)
    alias(libs.plugins.gatling)
    id 'maven-publish'
}

def jooqSrcDir = 'src/main/jooq-gen'

ext['junit-jupiter.version'] = libs.versions.junit.get()

java {
    sourceCompatibility = libs.versions.java.get()
}

repositories {
    mavenCentral()
}

// The 'dev' source set contains the development setup. These files are not included in the jar.
sourceSets {
    main {
        java.srcDirs += jooqSrcDir
    }
    dev {
        compileClasspath += main.output
        runtimeClasspath += main.output
    }
}

configurations {
    devImplementation.extendsFrom implementation
    devRuntimeOnly.extendsFrom runtimeOnly

    gatling.exclude group: "io.gatling.highcharts", module: "gatling-charts-highcharts"

    // exclude commons-logging to avoid runtime warnings like these
    // Standard Commons Logging discovery in action with spring-jcl:
    //    please remove commons-logging.jar from classpath in order to avoid potential conflicts
    configureEach {
        exclude group: "commons-logging", module: "commons-logging"
    }
}

dependencyManagement {
    gatling {
        dependencies {
            dependencySet(group: 'io.netty', version: '4.2.7.Final') {
                entry 'netty-codec-http'
                entry 'netty-codec'
                entry 'netty-handler'
                entry 'netty-buffer'
                entry 'netty-transport'
                entry 'netty-common'
                entry 'netty-transport-native-epoll'
            }
        }
    }
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-jetty"
    modules {
        module("org.springframework.boot:spring-boot-starter-tomcat") {
            replacedBy("org.springframework.boot:spring-boot-starter-jetty")
        }
    }
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-jooq"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-data-elasticsearch"
    implementation "org.springframework.boot:spring-boot-starter-data-redis"
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-cache"
    implementation "org.springframework.boot:spring-boot-starter-aop"
    implementation "org.springframework.boot:spring-boot-starter-mail"
    implementation "org.springframework.boot:spring-boot-starter-thymeleaf"
    implementation "org.springframework.security:spring-security-oauth2-client"
    implementation "org.springframework.security:spring-security-oauth2-jose"
    implementation "org.springframework.session:spring-session-jdbc"
    implementation "org.springframework.retry:spring-retry"

    implementation "org.apache.httpcomponents.client5:httpclient5"
    implementation "com.github.ben-manes.caffeine:caffeine"
    implementation 'com.github.ben-manes.caffeine:jcache'

    implementation "io.micrometer:micrometer-tracing"
    implementation "io.micrometer:micrometer-tracing-bridge-otel"
    implementation "io.opentelemetry:opentelemetry-exporter-zipkin"

    implementation libs.commons.lang3
    implementation libs.bouncycastle
    implementation libs.jedis

    implementation libs.bucket4j.spring
    implementation libs.bucket4j.redis

    implementation libs.jobrunr.spring

    implementation libs.flyway.core
    implementation libs.flyway.database.postgresql

    implementation libs.google.cloud.storage
    implementation libs.azure.storage.blob
    implementation libs.awssdk.s3
    implementation libs.awssdk.sts

    implementation libs.springdoc

    implementation libs.jackson.core
    implementation libs.jackson.annotations
    implementation libs.jackson.databind
    implementation libs.jackson.jaxb.annotations
    implementation libs.jackson.dataformat.xml
    implementation libs.jackson.dataformat.yaml
    implementation libs.jackson.dataformat.toml

    implementation libs.woodstox.core

    implementation libs.jaxb.api
    implementation libs.jaxb.impl

    implementation libs.tika.core
    implementation libs.re2j
    implementation libs.json.path
    implementation libs.loki.logback.appender

    runtimeOnly "io.micrometer:micrometer-registry-prometheus"
    runtimeOnly "org.postgresql:postgresql"
    jooqCodegen "org.postgresql:postgresql"

    devRuntimeOnly "org.springframework.boot:spring-boot-devtools"

    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation "org.springframework.security:spring-security-test"
    testImplementation libs.junit.jupiter.api
    testImplementation libs.testcontainers.elasticsearch
    testImplementation libs.testcontainers.localstack
    testImplementation libs.testcontainers.junit.jupiter

    testRuntimeOnly libs.junit.jupiter.engine
    testRuntimeOnly libs.testcontainers.postgresql

    gatling libs.gatling.core
    gatling libs.gatling.app
}

configurations.configureEach {
    resolutionStrategy {
        force 'org.apache.commons:commons-compress:1.26.0+'
    }
}

jooq {
    configuration {
        logging = "WARN"
        jdbc {
            driver = 'org.postgresql.Driver'
            url = 'jdbc:postgresql://localhost:5432/postgres'
            user = 'openvsx'
            password = 'openvsx'
        }
        generator {
            name = 'org.jooq.codegen.DefaultGenerator'
            database {
                name = 'org.jooq.meta.postgres.PostgresDatabase'
                inputSchema = 'public'
                includes = '.*'
                excludes = ''
            }
            target {
                packageName = 'org.eclipse.openvsx.jooq'
                directory = jooqSrcDir
            }
            generate {
                implicitJoinPathsToOne = false
                implicitJoinPathsToMany = false
                implicitJoinPathUnusedConstructors = false
                implicitJoinPathsUseTableNameForUnambiguousFKs = false
                implicitJoinPathsAsKotlinProperties = false
            }
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact ('scripts/run-server.sh') 
            artifact bootJar
        }
    }
}

tasks.register('runServer', JavaExec) {
    jvmArgs = ['--enable-native-access=ALL-UNNAMED'] // due to https://github.com/netty/netty/issues/15161
    classpath = sourceSets.dev.runtimeClasspath
    mainClass = 'org.eclipse.openvsx.RegistryApplication'
}

test {
    jvmArgs = ['--enable-native-access=ALL-UNNAMED'] // due to https://github.com/netty/netty/issues/15161
    useJUnitPlatform()
}

tasks.register('unitTests', Test) {
    description = 'Runs unit tests (excluding integration tests).'
    group = 'verification'
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
    useJUnitPlatform()
    exclude 'org/eclipse/openvsx/IntegrationTest.class'
    exclude 'org/eclipse/openvsx/cache/CacheServiceTest.class'
    exclude 'org/eclipse/openvsx/repositories/RepositoryServiceSmokeTest.class'
    exclude 'org/eclipse/openvsx/storage/AwsStorageServiceIntegrationTest.class'
}

tasks.register('s3IntegrationTests', Test) {
    description = 'Runs S3 integration tests using LocalStack (requires Docker/Podman).'
    group = 'verification'
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
    useJUnitPlatform()
    include 'org/eclipse/openvsx/storage/AwsStorageServiceIntegrationTest.class'

    // Set system properties for test configuration
    systemProperty 'spring.profiles.active', 's3-integration'
}

jacocoTestReport {
    reports {
        xml.required = true
    }

    dependsOn test // tests are required to run before generating the report
}

tasks.withType(ScalaCompile).configureEach {
    scalaCompileOptions.forkOptions.with {
        jvmArgs = ['-Xss100m'] // Scala compiler may require a larger stack size when compiling Gatling simulations
    }
}

apply from: 'dependencies.gradle'
apply from: 'test-extensions.gradle'
