buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.hibernate:hibernate-gradle-plugin:5.4.12.Final"
    }
}
plugins {
    id 'de.undercouch.download' version '4.0.4'
    id 'org.springframework.boot' version '2.2.6.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
}
apply plugin: 'org.hibernate.orm'

def versions = [
    java: '11',
    flyway: '6.3.3',
    springfox: '3.0.0',
    spdx: '2.2.1',
    gcloud: '1.113.0',
    guava: '28.2-jre',
    junit: '5.6.2',
    testcontainers: '1.14.3'
]
ext['junit-jupiter.version'] = versions.junit
sourceCompatibility = versions.java

repositories {
    mavenCentral()
}

// The 'dev' source set contains the development setup. These files are not included in the jar.
sourceSets {
    dev {
        compileClasspath += main.output
        runtimeClasspath += main.output
    }
}

configurations {
    devImplementation.extendsFrom implementation
    devRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-data-elasticsearch"
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.security:spring-security-oauth2-client"
    implementation "org.springframework.security:spring-security-oauth2-jose"
    implementation "org.springframework.session:spring-session-jdbc"
    implementation "org.flywaydb:flyway-core:${versions.flyway}"
    implementation "com.google.cloud:google-cloud-storage:${versions.gcloud}"
    implementation "io.springfox:springfox-boot-starter:${versions.springfox}"
    implementation "com.google.guava:guava:${versions.guava}"
    implementation("org.spdx:spdx-tools:${versions.spdx}") {
        exclude group: 'net.sf.saxon'
        exclude group: 'org.antlr', module: 'antlr'
        exclude group: 'org.apache.logging.log4j', module: 'log4j-slf4j-impl'
    }
    runtimeOnly "org.postgresql:postgresql"

    devRuntimeOnly "org.springframework.boot:spring-boot-devtools"

    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation "org.springframework.security:spring-security-test"
    testImplementation "org.testcontainers:elasticsearch:${versions.testcontainers}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${versions.junit}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${versions.junit}"
    testRuntimeOnly "org.testcontainers:postgresql:${versions.testcontainers}"
}

hibernate {
    enhance {
        enableLazyInitialization = true
        enableDirtyTracking = true
    }
}

task runServer(type: JavaExec) {
    classpath = sourceSets.dev.runtimeClasspath
    main = 'org.eclipse.openvsx.RegistryApplication'
}

test {
    useJUnitPlatform()
}

apply from: 'dependencies.gradle'
// apply from: 'test-extensions.gradle'
